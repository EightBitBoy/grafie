buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    //noinspection GroovyAssignabilityCheck
    dependencies {
        //noinspection GroovyAssignabilityCheck
        classpath 'com.gradle.publish:plugin-publish-plugin:0.9.7'
        //noinspection GroovyAssignabilityCheck
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
        //noinspection GroovyAssignabilityCheck
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'groovy'
//apply plugin: 'java-gradle-plugin' //https://docs.gradle.org/3.3/userguide/javaGradle_plugin.html
apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.jfrog.bintray'

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile 'org.codehaus.groovy:groovy-all:2.4.11'
    compile 'commons-codec:commons-codec:1.10'
    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
    testCompile 'org.apache.commons:commons-lang3:3.6'
}

group = 'de.eightbitboy'
version = '0.1.0-SNAPSHOT'

pluginBundle {
    website = 'https://github.com/EightBitBoy/grafie'
    vcsUrl = 'git@github.com:EightBitBoy/grafie.git'
    description = 'Grafie encrypts and decrypts files in your workspace. Use it to protect sensitive information in your public git repositories!'
    tags = ['crypto', 'cryptography', 'encryption', 'git', 'privacy']

    plugins {
        grafie {
            id = 'de.eightbitboy.grafie'
            displayName = 'Grafie - Gradle File Encryption Plugin'
            version = project.version
        }
    }
}

bintray {
    user = System.getenv('bintrayUser')
    key = System.getenv('bintrayKey')

    publish = true
    override = true

    pkg {
        repo = 'projects'
        name = 'grafie'
        licenses = ['Apache-2.0']
        vcsUrl = 'git@github.com:EightBitBoy/grafie.git'

        version {
            name = project.version
            released = new Date()
        }
    }
}

/*
https://github.com/bintray/gradle-bintray-plugin#readme
https://github.com/bintray/bintray-examples/tree/master/gradle-bintray-plugin-examples
*/
