buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    //noinspection GroovyAssignabilityCheck
    dependencies {
        //noinspection GroovyAssignabilityCheck
        classpath 'com.gradle.publish:plugin-publish-plugin:0.9.7'
        //noinspection GroovyAssignabilityCheck
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
        //noinspection GroovyAssignabilityCheck
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'
apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'jacoco'

repositories {
    jcenter()
    mavenCentral()
}

task('createClasspathManifest') {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

dependencies {
    compile gradleApi()
    compile 'org.codehaus.groovy:groovy-all:2.4.11'
    compile 'commons-codec:commons-codec:1.10'
    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
    testCompile 'org.apache.commons:commons-lang3:3.6'
    testCompile 'commons-io:commons-io:2.5'
    testRuntime files(createClasspathManifest)
}

group = 'de.eightbitboy'
version = '0.1.0'

pluginBundle {
    website = 'https://github.com/EightBitBoy/grafie'
    vcsUrl = 'git@github.com:EightBitBoy/grafie.git'
    description = 'Grafie encrypts and decrypts files in your workspace. Use it to protect sensitive information in your public git repositories!'
    tags = ['crypto', 'cryptography', 'encryption', 'git', 'privacy']

    plugins {
        grafie {
            id = 'de.eightbitboy.grafie'
            displayName = 'Grafie - Gradle File Encryption Plugin'
            version = project.version
        }
    }
}

publishing {
    publications {
        GrafiePublication(MavenPublication) {
            from components.java
            groupId project.group
            artifactId 'grafie'
            version project.version
        }
    }
}

bintray {
    user = findProperty('bintrayUser')
    key = findProperty('bintrayKey')

    publications = ['GrafiePublication']

    publish = true
    override = false

    pkg {
        repo = 'projects'
        name = 'grafie'
        licenses = ['Apache-2.0']
        vcsUrl = 'git@github.com:EightBitBoy/grafie.git'
        publicDownloadNumbers = true

        version {
            name = project.version
            released = new Date()
        }
    }
}

test {
    outputs.upToDateWhen { false }
    testLogging {
        exceptionFormat = 'full'
        events 'failed', 'passed', 'skipped', 'standardOut'
    }
}

/*
https://github.com/bintray/gradle-bintray-plugin#readme
https://github.com/bintray/bintray-examples/tree/master/gradle-bintray-plugin-examples
*/
